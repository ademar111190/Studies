apply plugin: "com.android.application"
apply plugin: "kotlin-kapt"
apply plugin: "kotlin-android"
apply plugin: "kotlin-android-extensions"

android {

    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    defaultConfig {
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode rootProject.ext.versionCode
        versionName rootProject.ext.versionName
        resConfigs "pt", "en", "es"
    }

    signingConfigs {
        release {
            keyAlias System.getenv("KEY_ALIAS")
            keyPassword System.getenv("KEY_PASSWORD")
            storeFile file("../../../Assets/keystore")
            storePassword System.getenv("STORE_PASSWORD")
        }
    }

    buildTypes {
        all {
            shrinkResources true
            minifyEnabled true

            proguardFile getDefaultProguardFile("proguard-android.txt")
            proguardFile "proguard-rules.pro"

            buildConfigField "String", "API_URL", "\"https://private-965f8-template19.apiary-mock.com/\""
        }
        release {
            signingConfig signingConfigs.release
        }
    }

    flavorDimensions "env"
    productFlavors {
        development {
            dimension "env"
            versionName rootProject.ext.versionName + " - development "
        }
        qa {
            dimension "env"
            versionName rootProject.ext.versionName + " - qa - " + "git rev-parse --short HEAD".execute().text.trim()
        }
        production {
            dimension "env"
        }
    }

    packagingOptions {
        exclude "META-INF/LICENSE"
        exclude "META-INF/LICENSE.txt"
        exclude "META-INF/NOTICE.txt"
        exclude "META-INF/*.properties"
        exclude "META-INF/core_debug.kotlin_module"
    }

    testOptions {
        unitTests.returnDefaultValues = true
    }

}

dependencies {

    kapt "com.google.dagger:dagger-compiler:$daggerVersion"
    kapt "com.github.bumptech.glide:compiler:$glideVersion"

    implementation project(":core")

    implementation "com.android.support:appcompat-v7:$supportVersion"
    implementation "com.android.support:design:$supportVersion"
    implementation "com.android.support:recyclerview-v7:$supportVersion"
    implementation "com.android.support:support-compat:$supportVersion"
    implementation "com.github.bumptech.glide:glide:$glideVersion"
    implementation "com.github.bumptech.glide:okhttp3-integration:$glideVersion"
    implementation "com.jakewharton.rxbinding2:rxbinding-appcompat-v7-kotlin:$rxBindingVersion"
    implementation "com.jakewharton.rxbinding2:rxbinding-design-kotlin:$rxBindingVersion"
    implementation "com.jakewharton.rxbinding2:rxbinding-kotlin:$rxBindingVersion"
    implementation "com.jakewharton.rxbinding2:rxbinding-recyclerview-v7:$rxBindingVersion"
    implementation "com.jakewharton.rxbinding2:rxbinding-support-v4:$rxBindingVersion"

    kaptTest "com.google.dagger:dagger-compiler:$daggerVersion"

    testImplementation "com.nhaarman:mockito-kotlin:$mockitoKotlinVersion"
    testImplementation "junit:junit:$jUnitVersion"
    testImplementation "org.assertj:assertj-core:$assertJVersion"
    testImplementation "org.mockito:mockito-core:$mockitoVersion"

}

task(variants) << {
    android.applicationVariants.all { variant ->
        print("Variant: ")
        println(variant.name)
    }
}

task(flavors) << {
    android.productFlavors.all { flavor ->
        print("Flavor: ")
        println(flavor.name)
    }
}

tasks.withType(Test) {
    afterTest { desc, result ->
        println "${result.resultType} on test ${desc.name} [${desc.className}]"
    }
}
